@return
be.nabu.libs.types.api.KeyValuePair [] properties ?= null

string processCode ?= null
string stateCode ?= null

# action codes you have achieved
string [] actionCodes ?= null

# running etc
string [] processState ?= null
string [] notProcessState ?= null

additionalProperties = series()

from = "~process_instances pi"
where = ""
if (processCode != null)
	from = from + " join ~process_versions pv on pi.process_version_id = pv.id join ~process_definitions pd on pv.process_definition_id = pd.id and pd.code = :processCode"
	additionalProperties = merge(additionalProperties, structure(key: "processCode", value: processCode))
	

if (stateCode != null)
	from = from + " join ~process_states ps on pi.current_state_id = ps.id and ps.code = :stateCode"
	additionalProperties = merge(additionalProperties, structure(key: "stateCode", value: stateCode))
	
if (processState != null)
	where = where + "pi.process_state in ("
	for (single : processState)
		if ($index > 0)
			where = where + ", "
		where = where + "'" + single + "'"
	where = where + ")"

if (notProcessState != null)
	if (where != "")
		where = where + " and "
	where = where + "pi.process_state not in ("
	for (single : notProcessState)
		if ($index > 0)
			where = where + ", "
		where = where + "'" + single + "'"
	where = where + ")"

for (property : properties)
	from = from + " join ~process_data pd" + $index + " on pd" + $index + ".process_instance_id = pi.id"
		+ " and pd" + $index + ".name = '" + property/key + "' and pd" + $index + ".value = :property" + $index + " and pd" + $index + ".active = true"
	additionalProperties = merge(additionalProperties, structure(key: "property" + $index, value: property/value))

for (actionCode : actionCodes)
	from = from + " join ~process_state_instances psi" + $index + " on psi" + $index + ".process_instance_id = pi.id"
	# localize binding
	stateBinding = "psi" + $index
	from = from + " join ~process_action_instances pai" + $index + " on pai" + $index + ".process_state_instance_id = " + stateBinding + ".id"
	# join to the definition to check the code
	from = from + " join ~process_actions pa" + $index + " on pa" + $index + ".id = pai" + $index + ".process_action_id and pa" + $index + ".code = :actionCode" + $index
	additionalProperties = merge(additionalProperties, structure(key: "actionCode" + $index, value: actionCode))

@return
string sql = "select distinct * from " + from

if (where != "")
	sql = sql + " where " + where
	
properties = additionalProperties


